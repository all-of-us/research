import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator
import org.pmiops.workbench.tooling.GenerateAPIListingTask
import org.pmiops.workbench.tooling.IncrementalHotSwapTask

def swaggerTemplateDir = 'src/main/resources'
def mergedApiSourceFile = 'src/main/resources/merged.yaml'
def clientApiSourceFile = 'src/main/resources/client_api.yaml'
def cbReviewApiSourceFile = 'src/main/resources/cb_review_api.yaml'
def cbSearchApiSourceFile = 'src/main/resources/cb_search_api.yaml'
def swaggerTargetFolder = 'src/generated/java'

task generateMergedSwagger(type: Exec) {
  commandLine "./build.rb", "merge-yaml"
}

task generateApi {
  inputs.file("$projectDir/$mergedApiSourceFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$mergedApiSourceFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('spring')
    config.setAdditionalProperties([
            'apiPackage'     : 'org.pmiops.workbench.api',
            'modelPackage'   : 'org.pmiops.workbench.model',
            'sourceFolder'   : swaggerTargetFolder,
            'useTags'        : 'true',
            // Generates delegate interfaces; used to make method annotations work without
            // having to copy them to our implementations.
            'delegatePattern': 'true'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def fireCloudApiFile = 'src/main/resources/fireCloud.yaml'

task generateFireCloudClient {
  inputs.file("$projectDir/$fireCloudApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$fireCloudApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.firecloud',
            'modelPackage'     : 'org.pmiops.workbench.firecloud.model',
            'apiPackage'       : 'org.pmiops.workbench.firecloud.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def notebooksApiFile = 'src/main/resources/notebooks.yaml'

task generateNotebooksClient {
  inputs.file("$projectDir/$notebooksApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$notebooksApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.notebooks',
            'modelPackage'     : 'org.pmiops.workbench.notebooks.model',
            'apiPackage'       : 'org.pmiops.workbench.notebooks.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def jiraApiFile = 'src/main/resources/jira.yaml'

task generateJiraClient {
  inputs.file("$projectDir/$jiraApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$jiraApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.jira',
            'modelPackage'     : 'org.pmiops.workbench.jira.model',
            'apiPackage'       : 'org.pmiops.workbench.jira.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def mandrillApiFile = 'src/main/resources/mandrill_api.yaml'

task generateMandrillClient {
  inputs.file("$projectDir/$mandrillApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$mandrillApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.mandrill',
            'modelPackage'     : 'org.pmiops.workbench.mandrill.model',
            'apiPackage'       : 'org.pmiops.workbench.mandrill.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

task generate_local_appengine_web_xml(type: Exec) {
  executable "bash"
  args "libproject/generate_appengine_web_xml.sh"
}

configurations {
  generatedCompile
  all {
    exclude group: 'com.google.guava', module:'guava-jdk5'
  }
}

project.ext.GAE_VERSION = '1.9.64'

buildscript {    // Configuration for building
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE'
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.5'
    classpath 'gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.12.0'
    classpath 'io.swagger:swagger-codegen:2.2.3'
    classpath 'org.owasp:dependency-check-gradle:3.1.0'
  }
}

ext['hibernate.version'] = '5.2.10.Final'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine-standard'  // App Engine tasks
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.swagger.generator'
apply plugin: 'org.owasp.dependencycheck'

configurations {
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
  bigquerytestCompile.extendsFrom testCompile
  bigquerytestRuntime.extendsFrom testRuntime
}

// Required for the swagger plugin's `validateSwagger` task
// See https://github.com/int128/gradle-swagger-generator-plugin
sourceSets {
  generated {
    compileClasspath = configurations.generatedCompile
  }
  main {
    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  test {
    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  integration {
    resources {
      srcDir "config/"
    }
    java {
      compileClasspath += main.output + test.output + generated.output
      runtimeClasspath += main.output + test.output + generated.output
      srcDir file('src/integration/java')
    }
  }
  bigquerytest {
    resources {
      srcDir "bigquerytest/resources"
      srcDir "config/"
      include "bigquery/**"
      include "cdm/**"
    }
    java {
      compileClasspath += main.output + test.output + generated.output
      runtimeClasspath += main.output + test.output + generated.output
      srcDir file('src/bigquerytest/java')
    }
  }
}

validateSwagger.dependsOn generateMergedSwagger
generateApi.dependsOn validateSwagger
ideaModule.dependsOn generateApi
compileGeneratedJava.dependsOn generateApi
ideaModule.dependsOn generateFireCloudClient
compileGeneratedJava.dependsOn generateFireCloudClient
ideaModule.dependsOn generateNotebooksClient
compileGeneratedJava.dependsOn generateNotebooksClient
ideaModule.dependsOn generateJiraClient
compileGeneratedJava.dependsOn generateJiraClient
ideaModule.dependsOn generateMandrillClient
compileGeneratedJava.dependsOn generateMandrillClient
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

war.dependsOn generate_local_appengine_web_xml

clean.doFirst {
  delete("${projectDir}/$swaggerTargetFolder")
}

repositories {   // repositories for Jar's you access in your code
  jcenter()
}

dependencies {

  // To show the dependency tree, try: ./project.rb gradle dependencies --configuration compile

  providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
  compile project(":common-api")
  compile 'com.google.api-client:google-api-client-appengine:1.23.0'
  compile 'mysql:mysql-connector-java:8.0.11'
  compile 'com.google.cloud.sql:mysql-socket-factory:1.0.10'
  compile "com.google.appengine:appengine:${GAE_VERSION}"
  compile "com.google.appengine:appengine-api-1.0-sdk:${GAE_VERSION}"
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile('org.springframework.boot:spring-boot-starter-data-jpa') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile('org.springframework.boot:spring-boot-starter-actuator') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile 'org.springframework.security:spring-security-web:4.2.2.RELEASE'
  compile 'org.hibernate:hibernate-core:5.2.10.Final'

  compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'
  compile 'com.fasterxml.jackson.core:jackson-core:2.9.5'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
  compile 'com.google.apis:google-api-services-admin-directory:directory_v1-rev86-1.23.0'
  compile 'com.google.apis:google-api-services-oauth2:v2-rev139-1.23.0'
  compile 'com.google.cloud:google-cloud-bigquery:1.51.0'
  compile 'com.google.cloud:google-cloud-storage:1.51.0'
  compile 'com.google.code.gson:gson:2.8.5'
  compile 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
  compile 'com.squareup.okhttp:okhttp:2.7.5'
  compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
  compile 'joda-time:joda-time:2.10'
  compile 'javax.inject:javax.inject:1'
  compile 'io.swagger:swagger-annotations:1.5.16'
  compile 'org.apache.commons:commons-lang3:3.0'
  compile 'com.google.guava:guava:26.0-jre'
  compile 'org.springframework.retry:spring-retry:1.1.5.RELEASE'

  // https://github.com/GoogleCloudPlatform/google-cloud-java/issues/1502
  compile 'org.json:json:20160810'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "com.google.appengine:appengine-api-stubs:${GAE_VERSION}"
  testCompile "com.google.appengine:appengine-tools-sdk:${GAE_VERSION}"
  testCompile 'com.google.truth:truth:0.42'
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile 'com.h2database:h2:1.4.194'
  testCompile 'org.liquibase:liquibase-core:3.5.3'
  testCompile 'org.bitbucket.radistao.test:before-after-spring-test-runner:0.1.0'

  // For Swagger generation. These should include dependencies found in the Swagger codegen
  // example here:
  // https://github.com/swagger-api/swagger-codegen/blob/v2.2.3/samples/client/petstore/spring-stubs/pom.xml
  generatedCompile 'org.springframework.boot:spring-boot-starter-data-rest'
  generatedCompile 'io.springfox:springfox-swagger2:2.6.1'
  generatedCompile 'io.springfox:springfox-swagger-ui:2.6.1'
  generatedCompile 'com.squareup.okhttp:okhttp:2.7.5'
  generatedCompile 'com.squareup.okhttp:logging-interceptor:2.7.5'
  generatedCompile 'com.google.code.gson:gson:2.8.5'
  generatedCompile 'joda-time:joda-time:2.10'
  generatedCompile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.9.6'
}

swaggerSources {
  workbench {
    inputFile = file("$projectDir/$mergedApiSourceFile")
  }
  client {
    inputFile = file("$projectDir/$clientApiSourceFile")
  }
  cbReview {
    inputFile = file("$projectDir/$cbReviewApiSourceFile")
  }
  cbSearch {
    inputFile = file("$projectDir/$cbSearchApiSourceFile")
  }
  firecloud {
    inputFile = file("$projectDir/$fireCloudApiFile")
  }
  notebooks {
    inputFile = file("$projectDir/$notebooksApiFile")
  }
  jira {
    inputFile = file("$projectDir/$jiraApiFile")
  }
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
}

task integration(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
  // Option to control size of stack trace:
  // jvmArgs '-XX:MaxJavaStackTraceDepth=10'
}

task bigquerytest(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  testClassesDirs = sourceSets.bigquerytest.output.classesDirs
  classpath = sourceSets.bigquerytest.runtimeClasspath
}

tasks.withType(Test) {
  testLogging {
    // Causes the correct line to be reported on an exception.
    exceptionFormat "full"
  }
  def verboseTestLogging=project.properties['verboseTestLogging'] ?: 'no'
  if (verboseTestLogging == 'yes') {
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
    }
  } else {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }
}

integration {
  // These tests should always run when requested since they consume and produce side-effects.
  outputs.upToDateWhen { false }
}

appengine {  // App Engine tasks configuration
  run {      // local (dev_appserver) configuration (standard environments only)
    port = 8081                 // default
    host = "0.0.0.0"
    // Allows you to attach a debugger on port 8001 when running locally.
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8001']
    // Give dev_appserver 2 minutes to start up when running appengineStart; by
    // default it will fail after 1 minute. (This is particularly a problem in CircleCI.)
    startSuccessTimeout = 120
  }

  // dmohs: You may see this message [1], but don't implement the suggested fix because it breaks
  // Spring.
  // [1] https://github.com/GoogleCloudPlatform/app-gradle-plugin/issues/100
  // stage {
  //    enableJarClasses = true
  // }

  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true              // default - & make this the current version
    // TODO(danrodney)
    //account = System.properties("account")
    //project = System.properties("project")
  }
}

group = 'org.pmiops.allofus.workbench'
version = '0.1.0'          // Version in generated output

sourceCompatibility = 1.8
targetCompatibility = 1.8

task incrementalHotSwap(type: IncrementalHotSwapTask) {
    inputDir = sourceSets.main.java.outputDir
}

incrementalHotSwap.dependsOn compileJava

task listProjectAPIs(type: GenerateAPIListingTask, dependsOn: generateMergedSwagger)
