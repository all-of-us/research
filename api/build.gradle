import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

def swaggerTemplateDir = 'src/main/resources'
def swaggerSourceFile = 'src/main/resources/workbench.yaml'
def swaggerTargetFolder = 'src/generated/java'

task generateApi {
  inputs.file("$projectDir/$swaggerSourceFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('spring')
    config.setAdditionalProperties([
            'apiPackage'     : 'org.pmiops.workbench.api',
            'modelPackage'   : 'org.pmiops.workbench.model',
            'sourceFolder'   : swaggerTargetFolder,
            'useTags'        : 'true',
            // Generates delegate interfaces; used to make method annotations work without
            // having to copy them to our implementations.
            'delegatePattern': 'true'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def fireCloudApiFile = 'src/main/resources/fireCloud.yaml'

task generateFireCloudClient {
  inputs.file("$projectDir/$fireCloudApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$fireCloudApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.firecloud',
            'modelPackage'     : 'org.pmiops.workbench.firecloud.model',
            'apiPackage'       : 'org.pmiops.workbench.firecloud.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

def notebooksApiFile = 'src/main/resources/notebooks.yaml'

task generateNotebooksClient {
  inputs.file("$projectDir/$notebooksApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$notebooksApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.notebooks',
            'modelPackage'     : 'org.pmiops.workbench.notebooks.model',
            'apiPackage'       : 'org.pmiops.workbench.notebooks.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java8'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

task generate_local_appengine_web_xml(type: Exec) {
  executable "bash"
  args "libproject/generate_appengine_web_xml.sh"
}

configurations {
  generatedCompile
}

buildscript {    // Configuration for building
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE'
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
    classpath 'gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.9.0'
    classpath 'io.swagger:swagger-codegen:2.2.3'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine-standard'  // App Engine tasks
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.swagger.generator'

configurations {
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
  bigquerytestCompile.extendsFrom testCompile
  bigquerytestRuntime.extendsFrom testRuntime
}

sourceSets {
  generated {
    compileClasspath = configurations.generatedCompile
  }
  main {
    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  test {

    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  integration {
    java {
      compileClasspath += main.output + test.output + generated.output
      runtimeClasspath += main.output + test.output + generated.output
      srcDir file('src/integration/java')
    }
  }
  bigquerytest {
    resources {
      srcDir "bigquerytest/resources"
      include "bigquery/**"
    }
    java {
      compileClasspath += main.output + test.output + generated.output
      runtimeClasspath += main.output + test.output + generated.output
      srcDir file('src/bigquerytest/java')
    }
  }
}

generateApi.dependsOn validateSwagger
ideaModule.dependsOn generateApi
compileGeneratedJava.dependsOn generateApi
ideaModule.dependsOn generateFireCloudClient
compileGeneratedJava.dependsOn generateFireCloudClient
ideaModule.dependsOn generateNotebooksClient
compileGeneratedJava.dependsOn generateNotebooksClient
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

war.dependsOn generate_local_appengine_web_xml

clean.doFirst {
  delete("${projectDir}/$swaggerTargetFolder")
}

repositories {   // repositories for Jar's you access in your code
  jcenter()
}

dependencies {

  // To show the dependency tree, try: ./project.rb gradle dependencies --configuration compile

  providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'

  compile 'com.google.api-client:google-api-client-appengine:1.23.0'
  compile 'mysql:mysql-connector-java:+'
  compile 'com.google.cloud.sql:mysql-socket-factory:+'
  compile 'com.google.appengine:appengine:+'
  compile 'com.google.appengine:appengine-api-1.0-sdk:+'
  compile('org.springframework.boot:spring-boot-starter-web:+') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile('org.springframework.boot:spring-boot-starter-data-jpa') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile('org.springframework.boot:spring-boot-starter-actuator:+') {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile 'org.springframework.security:spring-security-web:+'

  compile 'com.fasterxml.jackson.core:jackson-annotations:+'
  compile 'com.fasterxml.jackson.core:jackson-core:+'
  compile 'com.fasterxml.jackson.core:jackson-databind:+'
  compile 'com.google.apis:google-api-services-admin-directory:directory_v1-rev86-1.23.0'
  compile 'com.google.apis:google-api-services-oauth2:+'
  compile 'com.google.cloud:google-cloud-bigquery:0.30.0-beta'
  compile ('com.google.cloud:google-cloud-storage:1.8.0') {
    exclude group: 'com.google.guava', module:'guava-jdk5'
  }
  compile 'com.google.code.gson:gson:+'
  compile 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
  compile 'com.squareup.okhttp:okhttp:2.7.5'
  compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
  compile 'joda-time:joda-time:+'
  compile 'javax.inject:javax.inject:1'
  compile 'io.swagger:swagger-annotations:1.5.16'
  compile 'org.apache.commons:commons-lang3:3.0'
  compile 'com.blockscore:blockscore-java:4.0.2'
  compile 'com.google.guava:guava:22.0'
  compile 'org.hibernate:hibernate-ehcache:5.0.12.Final'

  // https://github.com/GoogleCloudPlatform/google-cloud-java/issues/1502
  compile 'org.json:json:20160810'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.+'
  testCompile 'com.google.truth:truth:+'
  testCompile 'org.springframework.boot:spring-boot-starter-test:+'
  testCompile 'com.h2database:h2:+'
  testCompile 'org.liquibase:liquibase-core:+'
  testCompile 'org.bitbucket.radistao.test:before-after-spring-test-runner:0.1.0'

  // For Swagger generation. These should include dependencies found in the Swagger codegen
  // example here:
  // https://github.com/swagger-api/swagger-codegen/blob/v2.2.3/samples/client/petstore/spring-stubs/pom.xml
  generatedCompile 'org.springframework.boot:spring-boot-starter-data-rest'
  generatedCompile 'io.springfox:springfox-swagger2:2.6.1'
  generatedCompile 'io.springfox:springfox-swagger-ui:2.6.1'
  generatedCompile 'com.squareup.okhttp:okhttp:2.7.5'
  generatedCompile 'com.squareup.okhttp:logging-interceptor:2.7.5'
  generatedCompile 'com.google.code.gson:gson:+'
  generatedCompile 'joda-time:joda-time:+'
  generatedCompile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:+'

}

swaggerSources {
  workbench {
    inputFile = file("$projectDir/$swaggerSourceFile")
  }
  firecloud {
    inputFile = file("$projectDir/$fireCloudApiFile")
  }
  notebooks {
    inputFile = file("$projectDir/$notebooksApiFile")
  }
}

task integration(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
  // Option to control size of stack trace:
  // jvmArgs '-XX:MaxJavaStackTraceDepth=10'
}

task bigquerytest(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  testClassesDirs = sourceSets.bigquerytest.output.classesDirs
  classpath = sourceSets.bigquerytest.runtimeClasspath
}

tasks.withType(Test) {
  testLogging {
    // Causes the correct line to be reported on an exception.
    exceptionFormat "full"
  }
  def verboseTestLogging=project.properties['verboseTestLogging'] ?: 'no'
  if (verboseTestLogging == 'yes') {
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
    }
  } else {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }
}

test {
  // Starting with a smaller minimum seems to help this task use memory more slowly. When Gradle
  // runs out of memory, it fails with this message:
  //   Process 'Gradle Test Executor 1' finished with non-zero exit value 137
  // and literally nothing else in terms of helpful debugging information.
  minHeapSize = '128m'
}

integration {
  // These tests should always run when requested since they consume and produce side-effects.
  outputs.upToDateWhen { false }
}

appengine {  // App Engine tasks configuration
  run {      // local (dev_appserver) configuration (standard environments only)
    port = 8081                 // default
    host = "0.0.0.0"
    // Allows you to attach a debugger on port 8001 when running locally.
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8001']
  }

  // dmohs: You may see this message [1], but don't implement the suggested fix because it breaks
  // Spring.
  // [1] https://github.com/GoogleCloudPlatform/app-gradle-plugin/issues/100
  // stage {
  //    enableJarClasses = true
  // }

  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true              // default - & make this the current version
    // TODO(danrodney)
    //account = System.properties("account")
    //project = System.properties("project")
  }
}

group = 'org.pmiops.allofus.workbench'
version = '0.1.0'          // Version in generated output

sourceCompatibility = 1.8
targetCompatibility = 1.8
