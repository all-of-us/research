apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine-standard'

buildscript {    // Configuration for building
    repositories {
        jcenter()    // Bintray's repository - a fast Maven Central mirror & more
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'    // latest App Engine Gradle tasks
    }
}

appengine {
    run {
        // configure the app to point to the right service directories
        services = [
                getExplodedAppDir(project(":ui")),
                getExplodedAppDir(project(":api"))
        ]
        jvmFlags = [
            '-Dcom.google.appengine.devappserver_module.default.port=8080',
            '-Dcom.google.appengine.devappserver_module.api.port=8081'
        ]
    }
}

// helper method to obtain correct directory and set up dependencies
def getExplodedAppDir(Project p) {
    // if not 'this' module, then do some setup.
    if (p != project) {
        // make sure we evaluate other modules first so we get the right value
        evaluationDependsOn(p.path)
        // make sure we build "run" depends on the other modules exploding wars
        project.tasks.appengineRun.dependsOn p.tasks.explodeWar
    }
    return p.tasks.explodeWar.explodedAppDirectory
}
